#include "hwgui.ch"
#include "inkey.ch"
#include "hbgtinfo.ch"

#define BUTTON_SIZE  50
#define TEXT_SIZE    20
#define BUTTON_SPACE 3
#define STYLE_BACK               15790720
#define STYLE_BTN_NORMAL         HStyle():New( { STYLE_BACK }, 1 )
#define STYLE_BTN_CLICK          HStyle():New( { STYLE_BACK }, 1,, 3, 0 )
#define STYLE_BTN_OVER           HStyle():New( { 16759929, 16771062 }, 1,, 2, 12164479 )
#define STYLE_BTN_ALL            { STYLE_BTN_NORMAL, STYLE_BTN_CLICK, STYLE_BTN_OVER }

PROCEDURE PTESHWGUIBrowse( cModule, cTitle, ... )

   LOCAL cnSQL := ADOLocal(), oTBrowse, cCampoKeyboard := "CODIGO", xValue

   hb_Default( @cTitle, "PESQUISA DE CIDADES" )
   WITH OBJECT cnSQL
      :Execute( "SELECT CINOME, CIUF, CIIBGE, IDCIDADE" + ;
         " FROM JPCIDADE" + ;
         " ORDER BY CINOME" )
      oTBrowse := { ;
         { "NOME", { || :String( "CINOME", 40 ) } }, ;
         { "UF",   { || :String( "CIUF", 2 ) } }, ;
         { "IBGE", { || :String( "CIIBGE", 7 ) } }, ;
         { "ID",   { || Str( :Number( "IDCIDADE" ), 6 ) } } }
      xValue := HWGUIBrowse( cTitle, cnSQL, oTBrowse, "CINOME", ;
         iif( cCampoKeyboard == "CODIGO", { || Str( :Number( "IDCIDADE" ), 6 ) }, { || :String( "CINOME", Len( GetActive():VarGet ) ) } ) )
      IF xValue != Nil
         MsgExclamation( Transform( xValue, "" ) )
      ENDIF
      :CloseRecordset()
   ENDWITH
   ( cModule )

   RETURN

FUNCTION HwguiBrowse( cTitle, cnSQL, oBrowseList, cFilterList, bCode )

   LOCAL oDlg, oBrowse, cFilter := "", lSelected := .F., xValue := Nil, oBtnList := {}

   hb_Default( @cFilter, "" )

   INIT DIALOG oDlg ;
      AT AppWindowRect( 1 ), AppWindowRect( 2 ) SIZE AppWindowRect( 3 ), AppWindowRect( 4 ) ;
      TITLE cTitle ;
      STYLE WS_DLGFRAME + WS_SYSMENU ;
      BACKCOLOR STYLE_BACK

   @ 11, 101 BROWSE ARRAY oBrowse ;
      SIZE oDlg:nWidth - 20, oDlg:nHeight - 140 STYLE WS_BORDER + WS_VSCROLL + WS_HSCROLL + DS_CENTER ;
      ON CLICK { || lSelected := .T., oDlg:Close() } ;
      ON KEYDOWN { | oBrw, nkey | oBrowseKey( oDlg, oBrw, nkey, @cFilter, lSelected, cFilterList ) }
   oBrowse:aArray := cnSQL
   BrowseSet( oBrowse, oBrowseList )
   CreateButtons( oDlg, oBrowse, @oBtnList )

   ACTIVATE DIALOG oDlg CENTER

   IF lSelected .AND. bCode != Nil
      xValue := Eval( bCode )
   ENDIF
   ( cFilterList )

   RETURN xValue

FUNCTION BrowseSet( oBrowse, oBrowseList )

   LOCAL oElement

   oBrowse:bSkip  := { | o, nSkip | ADOSkipper( o:aArray, nSkip ) }
   oBrowse:bGotop := { | o | o:aArray:MoveFirst() }
   oBrowse:bGobot := { | o | o:aArray:MoveLast() }
   oBrowse:bEof   := { | o | o:nCurrent > o:aArray:RecordCount() }
   oBrowse:bBof   := { | o | o:nCurrent == 0 }
   oBrowse:bRcou  := { | o | o:aArray:RecordCount() }
   oBrowse:bRecno := { | o | o:aArray:AbsolutePosition() }
   oBrowse:bRecnoLog := oBrowse:bRecno
   oBrowse:bGOTO  := { | o, n | (o), o:aArray:Move( n - 1, 1 ) }

   FOR EACH oElement IN oBrowseList
      ADD COLUMN oElement[ 2 ] TO oBrowse HEADER oElement[ 1 ] LENGTH Int( Len( Transform( Eval( oElement[ 2 ] ), "" ) ) * 1.2 ) + 1
   NEXT

   RETURN Nil

FUNCTION ADOSkipper( cnSQL, nSkip )

   LOCAL nRec := cnSQL:AbsolutePosition()

   IF ! cnSQL:Eof()
      cnSQL:Move( nSkip )
      IF cnSQL:Eof()
         cnSQL:MoveLast()
      ENDIF
      IF cnSQL:Bof()
         cnSQL:MoveFirst()
      ENDIF
   ENDIF

   RETURN cnSQL:AbsolutePosition() - nRec

STATIC FUNCTION oBrowseKey( oDlg, oBrowse, nKey, cFilter, lSelected, cFilterList )

   nKey := hb_KeyStd( nKey )
   DO CASE
   CASE nKey == K_ENTER .OR. nKey == K_ESC
      IF nKey == K_ENTER
         lSelected := .T.
      ENDIF
      oDlg:Close()
      RETURN .F.
   CASE IsAscChar( nKey )
      IF nKey == K_BS
         IF Len( cFilter ) > 0
            cFilter := Left( cFilter, Len( cFilter ) - 1 )
         ENDIF
      ELSE
         cFilter += Upper( Chr( nKey ) )
      ENDIF
      oBrowse:aArray:Filter( iif( Empty( cFilter ), "", cFilterList + " LIKE '%" + cFilter + "%'" ) )
      IF oBrowse:aArray:Eof() .AND. Len( cFilter ) > 0
         cFilter := Substr( cFilter, 1, Len( cFilter ) - 1 )
         oBrowse:aArray:Filter( iif( Empty( cFilter ), "", cFilterList + " LIKE '%" + cFilter + "%'" ) )
      ENDIF
      oBrowse:Refresh()
   ENDCASE

   RETURN .T.

STATIC FUNCTION IsAscChar( nKey )

   DO CASE
   CASE nKey == VK_BACK
   CASE nKey >= Asc( "A" ) .AND. nKey <= Asc( "Z" )
   CASE nKey >= Asc( "a" ) .AND. nKey <= Asc( "z" )
   CASE nKey >= Asc( "0" ) .AND. nKey <= Asc( "9" )
   CASE hb_AScan( { " " }, { | e | nKey == Asc( e ) } ) != 0
   OTHERWISE
      RETURN .F.
   ENDCASE

   RETURN .T.

STATIC FUNCTION CreateButtons( oDlg, oBrowse, oBtnList )

   LOCAL nRow, nCol, oBtn, nRowLine := 1
   LOCAL acOptions := { ;
      { "First",      "icoGoTop",    { || oBrowse:Top() } }, ;
      { "PrevPage",   "icoGoPgUp",   { || oBrowse:PageUp() } }, ;
      { "Previous",   "icoGoUp",     { || oBrowse:LineUp() } }, ;
      { "Next",       "icoGoDown",   { || oBrowse:LineDown() } }, ;
      { "NextPage",   "icoGoPgDn",   { || oBrowse:PageDown() } }, ;
      { "Last",       "icoGoBottom", { || oBrowse:Bottom() } }, ;
      { "Filter",     "icoFilter",   { || Nil } }, ;
      { "Exit",       "icoDoor",     { || oDlg:Close() } } }

   nCol := 10
   nRow := 10
   oBtnList := {}
   FOR EACH oBtn IN acOptions
      @ nCol, nRow OWNERBUTTON oBtn OF oDlg SIZE BUTTON_SIZE, BUTTON_SIZE ;
         ON CLICK oBtn[ 3 ] ;
         BITMAP ;
         /* AppLoadImage( cIcon, WIN_IMAGE_ICON, 40, 40 ) */ ;
         HICON():AddResource( oBtn[ 2 ], BUTTON_SIZE - TEXT_SIZE, BUTTON_SIZE - TEXT_SIZE ) COORDINATES 1, 1, BUTTON_SIZE - TEXT_SIZE, BUTTON_SIZE - TEXT_SIZE ;
         TEXT oBtn[ 1 ] COORDINATES 5, BUTTON_SIZE - TEXT_SIZE, BUTTON_SIZE - 5, TEXT_SIZE ;
         Tooltip oBtn[ 1 ]
      oBtn:aStyle := STYLE_BTN_ALL
      IF nCol > AppWindowRect( 3 ) - AppWindowRect( 1 ) - 10 - BUTTON_SIZE - BUTTON_SPACE
         nRowLine += 1
         nRow += BUTTON_SIZE + BUTTON_SPACE
         nCol := AppWindowRect( 3 ) - AppWindowRect( 1 ) - BUTTON_SIZE - BUTTON_SPACE
      ENDIF
      AAdd( oBtnList, oBtn )
      nCol += iif( nRowLine == 1, 1, -1 ) * ( BUTTON_SIZE + BUTTON_SPACE )
   NEXT

   RETURN Nil
